@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'Return Maximum Counter'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
define view entity ZAPR_I_EDIDS_MAX as select from edids {
    key docnum,
    max( countr ) as countr
} group by docnum


@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'Status Record'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
define view entity ZAPR_I_EDIDS as select from edids
inner join ZAPR_I_EDIDS_MAX as _MaxRecord
on edids.docnum = _MaxRecord.docnum and edids.countr = _MaxRecord.countr
 {
    key edids.docnum,
    key edids.countr,
    edids.status,
    edids.statxt,
    edids.stapa1,
    edids.stapa2,
    edids.stapa3,
    edids.stapa4,
    edids.stamid,
    edids.stamno
}



@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'IDoc Control and Status Reocrd'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
define view entity ZAPR_CO_IDOC as select from edidc
association[1] to ZAPR_I_EDIDS as _Status
on edidc.docnum = _Status.docnum 
 {
    //edidc
    key docnum,
    status,
    credat,
    cretim,
    _Status.statxt,
    _Status.stapa1,
    _Status.stapa2,
    _Status.stapa3,
    _Status.stapa4,
    _Status.stamid,
    _Status.stamno,
    case
        when _Status.stamid = 'CASH_MSG' and _Status.stamno = '095' then 'Not Routed'
        when _Status.stamid = 'V1' and _Status.stamno = '384' then 'Missing Part No'
        when _Status.stamid = 'TAX_TXJCD' and _Status.stamno = '101' then 'Address Error'
        when _Status.stamid = 'VG' and _Status.stamno = '140' then 'Ship to Loc Error'
        else 'Others' end as ErrorCategory,
    case when _Status.stamid = 'CASH_MSG' and _Status.stamno = '095' then cast(1 as abap.int4)
         else 0 end as NotRouted,
    case when _Status.stamid = 'V1' and _Status.stamno = '384' then cast(1 as abap.int4)
        else 0 end as MissingPart,
    case when _Status.stamid = 'TAX_TXJCD' and _Status.stamno = '101' then cast(1 as abap.int4)
        else 0 end as AddressError,
    case when _Status.stamid = 'VG' and _Status.stamno = '140' then cast(1 as abap.int4)
        else 0 end as ShipToLoc,
    case when ( _Status.stamid = 'CASH_MSG' and _Status.stamno = '095' ) or
        ( _Status.stamid = 'V1' and _Status.stamno = '384' ) or
        ( _Status.stamid = 'TAX_TXJCD' and _Status.stamno = '101' ) or 
        ( _Status.stamid = 'VG' and _Status.stamno = '140' )  then 0
        else cast(1 as abap.int4) end as Others
        
}



@AbapCatalog.sqlViewName: 'ZAPRCIDDASH'
@AbapCatalog.compiler.compareFilter: true
@AbapCatalog.preserveKey: true
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'IDoc Dashboard'
@OData.publish: true
@UI.chart: [{
                qualifier: 'modi',
                title: 'Issues %',
                chartType: #DONUT,
                description: 'Anubhav Trainings',
                measures: ['TotalErros'],
                dimensions: ['ErrorCategory'],
                dimensionAttributes: [{dimension: 'ErrorCategory', role: #CATEGORY }],
                measureAttributes: [{measure: 'TotalErros', asDataPoint: true, role: #AXIS_1 }]
                
 },
 {
                qualifier: 'putin',
                title: 'Issues %',
                chartType: #COLUMN,
                description: 'Anubhav Trainings',
                measures: ['TotalErros'],
                dimensions: ['ErrorCategory'],
                dimensionAttributes: [{dimension: 'ErrorCategory', role: #CATEGORY }],
                measureAttributes: [{measure: 'TotalErros', asDataPoint: true, role: #AXIS_1 }]
 }]
define view ZAPR_C_IDOC_DASHBOARD as select from ZAPR_CO_IDOC {
    @UI.lineItem: [{position: 10 }]
    key ErrorCategory,
    @UI.lineItem: [{position: 20, type: #AS_DATAPOINT }]
    @UI.dataPoint:{
        criticalityCalculation: {
            improvementDirection: #MINIMIZE,
            deviationRangeHighValue: 2500,
            deviationRangeLowValue: 2500,
            toleranceRangeHighValue: 1500,
            toleranceRangeLowValue: 1500
        }
    }
    sum( case ErrorCategory
            when 'Not Routed' then NotRouted
            when 'Missing Part No' then MissingPart
            when 'Address Error' then  AddressError
            when 'Ship to Loc Error' then ShipToLoc
            when 'Others' then  Others end
     ) as TotalErros
} group by ErrorCategory



@AbapCatalog.sqlViewName: 'ZAPRCLRPI'
@AbapCatalog.compiler.compareFilter: true
@AbapCatalog.preserveKey: true
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'List Report CDS View'
@OData.publish: true
define view ZAPR_C_IDOC_LRP as select from ZAPR_CO_IDOC as doc
association[1] to ZAPR_TF_IDOC_MSG as _Message
on doc.docnum = _Message.docnum 
 {
    //ZAPR_CO_IDOC
    @UI.selectionField: [{position: 10 }]
    @UI.lineItem: [{position: 10 }]
    @UI.identification: [{position: 10 }]
    key docnum,
    @UI.selectionField: [{position: 20 }]
    @UI.lineItem: [{position: 20 }]
    @UI.identification: [{position: 20 }]
    status,
    @UI.lineItem: [{position: 30 }]
    @UI.identification: [{position: 30 }]
    credat,
    cretim,
    @UI.lineItem: [{position: 40 }]
    @UI.identification: [{position: 40 }]
    _Message(p_clnt : $session.client).statxt,
    @UI.lineItem: [{position: 50 }]
    @UI.selectionField: [{position: 30 }]
    @UI.identification: [{position: 50 }]
    @EndUserText.label: 'Error Category'
    ErrorCategory
}


@EndUserText.label: 'Extract Message Texts'
define table function ZAPR_TF_IDOC_MSG
with parameters 
@Environment.systemField: #CLIENT
p_clnt : abap.clnt
returns {
  client : abap.clnt;
  docnum : edi_docnum;
  statxt : edi_statx_;  
}
implemented by method zcl_apr_idoc=>extract_message;


class zcl_apr_idoc definition
  public
  final
  create public .

  public section.
    interfaces if_amdp_marker_hdb.
    class-methods extract_message for table function ZAPR_TF_IDOC_MSG.
  protected section.
  private section.
endclass.



class zcl_apr_idoc implementation.
  method extract_message by database function for hdb language sqlscript
  using edids.

       --Step 1: get the most latest counter for status record
       lt_unprocessed = select mandt as client, docnum, countr, statxt, stapa1, stapa2,
                               stapa3, stapa4 from edids where
                               ( docnum, countr ) in ( select docnum, max( countr ) from edids
                                group by docnum
                               );
       --Step 2: Start our logic to replace the occurrences of & systematically
       lt_msg1 = select client, docnum, countr, stapa2,
                               stapa3, stapa4,
                               case when statxt like '%&%'
                               then concat( concat ( substr_before( statxt, '&'), stapa1 ), substr_after( statxt, '&' ) )
                               else statxt end as statxt
                               from :lt_unprocessed;

       lt_msg2 = select client, docnum, countr,
                               stapa3, stapa4,
                               case when statxt like '%&%'
                               then concat( concat ( substr_before( statxt, '&'), stapa2 ), substr_after( statxt, '&' ) )
                               else statxt end as statxt
                               from :lt_msg1;

       lt_msg3 = select client, docnum, countr,
                               stapa4,
                               case when statxt like '%&%'
                               then concat( concat ( substr_before( statxt, '&'), stapa3 ), substr_after( statxt, '&' ) )
                               else statxt end as statxt
                               from :lt_msg2;

       --Step 3: Send the final message text out
        return select client, docnum,
                               case when statxt like '%&%'
                               then concat( concat ( substr_before( statxt, '&'), stapa4 ), substr_after( statxt, '&' ) )
                               else statxt end as statxt
                               from :lt_msg3;

  endmethod.

endclass.
